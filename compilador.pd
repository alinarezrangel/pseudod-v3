utilizar bepd/builtins
utilizar bepd/utilidades/texto

utilizar tokenizador como Tok
utilizar ast como AST
utilizar parser como Parser
utilizar caminaNodos como CN

clase Ámbito
    metodo estatico vacío
    metodo estatico desdeArreglo: lst
    metodo estatico conValores: ...vals

    atributo ámbitoPadre

    metodo inicializar

    metodo agregar: nombre, valor
    metodo fijarValor: nombre, valor
    metodo eliminar: nombre
    metodo obtener: nombre

    metodo crearSubámbito

    metodo comoTexto
finclase

atributos Ámbito#_mapeo

metodo estatico Ámbito#vacío
    devolver yo#crear
finmetodo

metodo estatico Ámbito#desdeArreglo: lst
    variable inst
    fijar inst a yo#crear
    fijar inst#_mapeo a Diccionario#desdeArreglo: lst
    devolver inst
finmetodo

metodo estatico Ámbito#conValores: ...vals
    variable inst
    fijar inst a yo#crear
    fijar inst#_mapeo a __EnviarMensaje: Diccionario, {desdeArreglo}, vals
    devolver inst
finmetodo

metodo Ámbito#inicializar
    fijar yo#_mapeo a Diccionario#vacío
    fijar yo#ámbitoPadre a NULO
finmetodo

metodo Ámbito#agregar: nombre, valor
    yo#_mapeo#fijarEn: nombre, valor
finmetodo

metodo Ámbito#fijarValor: nombre, valor
    yo#_mapeo#fijarEn: nombre, valor
finmetodo

metodo Ámbito#eliminar: nombre
    yo#_mapeo#eliminar: nombre
finmetodo

metodo Ámbito#obtener: nombre
    si yo#_mapeo#contiene: nombre
        devolver yo#_mapeo#en: nombre
    sino
        si EsNulo: yo#ámbitoPadre
            __FallarConMensaje: ({El ámbito no contiene el nombre ~T}#formatear: nombre)
        sino
            devolver yo#ámbitoPadre#obtener: nombre
        finsi
    finsi
finmetodo

metodo Ámbito#crearSubámbito
    variable sub
    fijar sub a Ámbito#vacío
    fijar sub#ámbitoPadre a yo
    devolver sub
finmetodo

metodo Ámbito#comoTexto
    devolver {(Ámbito: valores = ~t, padre = ~t)}#formatear: yo#_mapeo, yo#ámbitoPadre
finmetodo

clase LlaveResoluciónDeNombres
    metodo comoTexto
finclase

metodo LlaveResoluciónDeNombres#comoTexto
    devolver {<LLAVE_RESOLUCIÓN_DE_NOMBRES>}
finmetodo

variable LLAVE_RESOLUCIÓN_DE_NOMBRES
fijar LLAVE_RESOLUCIÓN_DE_NOMBRES a LlaveResoluciónDeNombres#crear

variable GID_NUM
fijar GID_NUM a 0

procedimiento GenerarIdDeNombre
    fijar GID_NUM a GID_NUM + 1
    devolver GID_NUM
finprocedimiento

procedimiento FijarNombreResuelto: nodo, id
    nodo#fijarMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, {nombreResuelto}, id
finprocedimiento

clase ResoluciónDeNombresCNImpl hereda CN#CaminaNodos
    metodo inicializar

    metodo finalizar: nodo, llave
finclase

atributo ResoluciónDeNombresCNImpl#_nombres,
         ResoluciónDeNombresCNImpl#_porResolver

metodo ResoluciónDeNombresCNImpl#inicializar
    fijar yo#_nombres a Ámbito#vacío
    fijar yo#_porResolver a Arreglo#vacio
finmetodo

metodo ResoluciónDeNombresCNImpl#crearSubámbito
    variable sub
    fijar sub a ResoluciónDeNombresCNImpl#crear
    fijar sub#_nombres#ámbitoPadre a yo#_nombres
    devolver sub
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarVariable: nodo
    ParaCadaElemento: nodo#nombres, procedimiento: var
        variable id
        fijar id a GenerarIdDeNombre
        yo#_nombres#agregar: var#nombre, id
        FijarNombreResuelto: var, id
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarFijar: nodo
    yo#visitar: nodo#objetivo
    yo#visitar: nodo#valor
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarEscribir: nodo
    yo#visitar: nodo#valor
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarNl: nodo
    [ Nada que hacer. ]
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarClase: nodo
    procedimiento ResolverTodoEnLista: ls
        ParaCadaElemento: ls, procedimiento: nodo
            FijarNombreResuelto: nodo, (yo#_nombres#obtener: nodo#nombre)
        finprocedimiento
    finprocedimiento

    variable nomclsid
    fijar nomclsid a GenerarIdDeNombre
    yo#_nombres#agregar: nodo#nombre#nombre, nomclsid
    FijarNombreResuelto: nodo#nombre, nomclsid

    ResolverTodoEnLista: (Arreglo#crearCon: nodo#claseBase)
    ResolverTodoEnLista: nodo#extiendeClases
    ResolverTodoEnLista: nodo#implementaClases

    ParaCadaElemento: nodo#declaraciones, procedimiento: declr
        yo#visitar: declr
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarDeclaraciónDeAtributosEnClase: nodo
    [ Nada que hacer. ]
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarDeclaraciónDeMétodoEnClase: nodo
    [ En teoría podríamos marcar los parámetros de la declaración, pero ya que
    nunca habrán usos de estos no lo veo mucho sentido. Por esto, no le voy a
    agregar el atributo nombreResuelto a los parámetros. ]
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarImplementa: nodo
    FijarNombreResuelto: nodo#nombre, (yo#_nombres#obtener: yo#nombre#nombre)

    ParaCadaElemento: nodo#definiciones, procedimiento: declr
        yo#visitar: declr
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarDefineAtributosEnClase: nodo
    [ De nuevo, nada que hacer. ]
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarDefineMétodoEnClase: nodo
    variable rdn
    fijar rdn a yo#crearSubámbito

    ParaCadaElemento: nodo#parámetros, procedimiento: param
        variable id
        fijar id a GenerarIdDeNombre
        rdn#_nombres#agregar: param#nombre, id
        FijarNombreResuelto: param, id
    finprocedimiento

    yo#_porResolver#agregarAlFinal: procedimiento
        ParaCadaElemento: nodo#cuerpo, procedimiento: stmt
            rdn#visitar: stmt
        finprocedimiento

        rdn#finalizar: nodo, NULO
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarFunción: nodo
    variable rdn
    fijar rdn a yo#crearSubámbito

    variable idfunc
    fijar idfunc a GenerarIdDeNombre
    FijarNombreResuelto: nodo#nombre, idfunc
    yo#_nombres#agregar: nodo#nombre#nombre, idfunc

    ParaCadaElemento: nodo#parámetros, procedimiento: param
        variable id
        fijar id a GenerarIdDeNombre
        rdn#_nombres#agregar: param#nombre, id
        FijarNombreResuelto: param, id
    finprocedimiento

    yo#_porResolver#agregarAlFinal: procedimiento
        ParaCadaElemento: nodo#cuerpo, procedimiento: stmt
            rdn#visitar: stmt
        finprocedimiento

        rdn#finalizar: nodo, NULO
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarNecesitas: nodo
    yo#visitar: nodo#expresión
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarDevolver: nodo
    yo#visitar: nodo#expresión
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarSi: nodo
    variables siVerdadero, siFalso
    yo#visitar: nodo#condicional

    fijar siVerdadero a yo#crearSubámbito
    ParaCadaElemento: nodo#siVerdadero, procedimiento: nodo
        siVerdadero#visitar: nodo
    finprocedimiento
    siVerdadero#finalizar: nodo, {nombresDefinídosSiVerdadero}

    fijar siFalso a yo#crearSubámbito
    ParaCadaElemento: nodo#siFalso, procedimiento: nodo
        siFalso#visitar: nodo
    finprocedimiento
    siFalso#finalizar: nodo, {nombresDefinídosSiFalso}
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarMientras: nodo
    variables cuerpo
    yo#visitar: nodo#condicional

    fijar cuerpo a yo#crearSubámbito
    ParaCadaElemento: nodo#cuerpo, procedimiento: nodo
        cuerpo#visitar: nodo
    finprocedimiento
    cuerpo#finalizar: nodo, NULO
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarMétodo: nodo
    yo#visitar: nodo#deClase
    yo#visitarFunción: nodo
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarAtributos: nodo
    yo#visitar: nodo#deClase
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarIdentificador: nodo
    FijarNombreResuelto: nodo, (yo#_nombres#obtener: nodo#nombre)
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarNúmeroLiteral: nodo
    [ Nada que hacer... ]
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarTextoLiteral: nodo
    [ Nada que hacer... ]
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarLlamarProcedimiento: nodo
    yo#visitar: nodo#proc
    ParaCadaElemento: nodo#argumentos, procedimiento: arg
        yo#visitar: arg
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarEnviarMensaje: nodo
    yo#visitar: nodo#objeto
    ParaCadaElemento: nodo#argumentos, procedimiento: arg
        yo#visitar: arg
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarOperador: nodo
    yo#visitar: nodo#lhs
    yo#visitar: nodo#rhs
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarNoLlamar: nodo
    yo#visitar: nodo#base
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarAutoejecutar: nodo
    yo#visitar: nodo#expr
    ParaCadaElemento: nodo#argumentos, procedimiento: arg
        yo#visitar: arg
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarFunciónAnónima: nodo
    variable rdn
    fijar rdn a yo#crearSubámbito

    ParaCadaElemento: nodo#parámetros, procedimiento: param
        variable id
        fijar id a GenerarIdDeNombre
        rdn#_nombres#agregar: param#nombre, id
        FijarNombreResuelto: param, id
    finprocedimiento

    si nodo#esMétodo
        variable id
        fijar id a GenerarIdDeNombre
        rdn#_nombres#agregar: {yo}, id
        FijarNombreResuelto: nodo, id
    finsi

    yo#_porResolver#agregarAlFinal: procedimiento
        ParaCadaElemento: nodo#cuerpo, procedimiento: stmt
            rdn#visitar: stmt
        finprocedimiento

        rdn#finalizar: nodo, NULO
    finprocedimiento
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarSonIguales: nodo
    yo#visitar: nodo#lhs
    yo#visitar: nodo#rhs
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarReferenciar: nodo
    yo#visitar: nodo#nombre
finmetodo

metodo ResoluciónDeNombresCNImpl#visitarNo: nodo
    yo#visitar: nodo#nombre
finmetodo

metodo ResoluciónDeNombresCNImpl#finalizar: nodo, llave
    ParaCadaElemento: yo#_porResolver, procedimiento: callback
        %callback
    finprocedimiento

    si no EsNulo: nodo
        si EsNulo: llave
            fijar llave a {nombresDefinídos}
        finsi
        nodo#fijarMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, llave, yo#_nombres
    finsi
finmetodo

procedimiento ResolverNombres: ast
    variable cn
    fijar cn a ResoluciónDeNombresCNImpl#crear
    ParaCadaElemento: ast, procedimiento: nodo
        cn#visitar: nodo
    finprocedimiento
    cn#finalizar: NULO, NULO
    devolver cn#_nombres
finprocedimiento

funcion ObtenerNombresDefinídos: nodo
    devolver nodo#obtenerMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, {nombresDefinídos}
finfuncion

funcion EscaparParaLua: texto
    variable res
    fijar res a {"} [ " ]
    ParaCadaElemento: texto, procedimiento: car
        si car = {"} [ " ]
            fijar res a res#concatenar: {\"} [ " ]
        sino
            si car = {~%}#formatear
                fijar res a res#concatenar: {~%}#formatear
            sino
                fijar res a res#concatenar: car
            finsi
        finsi
    finprocedimiento
    devolver res#concatenar: {"} [ " ]
finfuncion

funcion NombreResueltoDe: id
    devolver id#obtenerMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, {nombreResuelto}
finfuncion

funcion IdentificadorDe: id
    devolver {_}#concatenar: (NombreResueltoDe: id)#comoTexto
finfuncion

clase CompiladorSimpleALua hereda CN#CaminaNodos
    metodo estatico crearSubámbitoDesdeNodo: nodo

    metodo inicializar: ámbito

    metodo iniciar
finclase

atributo CompiladorSimpleALua#_ámbito

metodo estatico CompiladorSimpleALua#crearSubámbitoDesdeNodo: nodo
    devolver CompiladorSimpleALua#crear: (nodo#obtenerMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, {nombresDefinídos})
finmetodo

metodo CompiladorSimpleALua#inicializar: ámbito
    fijar yo#_ámbito a ámbito
finmetodo

metodo CompiladorSimpleALua#iniciar
    variable res
    fijar res a Arreglo#vacio
    yo#_ámbito#_mapeo#paraCadaPar: procedimiento: nombreEnPseudoD, id
        res#agregarAlFinal: ({local _~t}#formatear: id)
    finprocedimiento
    devolver Unir: res, {~%}#formatear
finmetodo

metodo CompiladorSimpleALua#visitarVariable: nodo
    devolver {}
finmetodo

metodo CompiladorSimpleALua#visitarFijar: nodo
    necesitas EsInstancia: nodo#objetivo, AST#NodoIdentificador
    variable obj, expr
    fijar obj a IdentificadorDe: nodo#objetivo
    fijar expr a yo#visitar: nodo#valor
    devolver {~t = ~t}#formatear: obj, expr
finmetodo

metodo CompiladorSimpleALua#visitarEscribir: nodo
    devolver {rt.escribir(~t)}#formatear: (yo#visitar: nodo#valor)
finmetodo

metodo CompiladorSimpleALua#visitarNl: nodo
    devolver {rt.nl()}
finmetodo

metodo CompiladorSimpleALua#visitarClase: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarDeclaraciónDeAtributosEnClase: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarDeclaraciónDeMétodoEnClase: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarImplementa: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarDefineAtributosEnClase: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarDefineMétodoEnClase: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#expresiónDeFunción: nombreResueldoDeYo, parámetros, cuerpo
    variables cods, params, met
    fijar cods a Arreglo#vacio

    fijar params a Unir: (Mapear: parámetros, &IdentificadorDe), {, }

    si no EsNulo: nombreResueldoDeYo
        fijar met a {_~t}#formatear: nombreResueldoDeYo
        si parámetros#longitud > 0
            fijar params a {, }#concatenar: params
        finsi
    sino
        fijar met a {}
    finsi
    cods#agregarAlFinal: ({function(~t~t)}#formatear: met, params)
    cods#agregarAlFinal: yo#iniciar

    ParaCadaElemento: cuerpo, procedimiento: stmt
        cods#agregarAlFinal: (yo#visitar: stmt)
    finprocedimiento

    cods#agregarAlFinal: {end}

    devolver Unir: cods, {~%}#formatear
finmetodo

metodo CompiladorSimpleALua#visitarFunción: nodo
    variables cods, id, subyo
    fijar subyo a CompiladorSimpleALua#crearSubámbitoDesdeNodo: nodo
    fijar id a IdentificadorDe: nodo#nombre
    fijar cods a Arreglo#vacio
    cods#agregarAlFinal: ({~t = ~t}#formatear:
        id,
        (subyo#expresiónDeFunción: NULO, nodo#parámetros, nodo#cuerpo)
    )
    devolver Unir: cods, {~%}#formatear
finmetodo

metodo CompiladorSimpleALua#visitarNecesitas: nodo
    devolver {assert(~t)}#formatear: (yo#visitar: nodo#expresión)
finmetodo

metodo CompiladorSimpleALua#visitarDevolver: nodo
    devolver {return ~t}#formatear: (yo#visitar: nodo#expresión)
finmetodo

metodo CompiladorSimpleALua#visitarSi: nodo
    variables cods, subSiVerdadero, subSiFalso

    fijar subSiVerdadero a
        CompiladorSimpleALua#crear:
            (nodo#obtenerMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, {nombresDefinídosSiVerdadero})
    fijar subSiFalso a
        CompiladorSimpleALua#crear:
            (nodo#obtenerMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, {nombresDefinídosSiFalso})

    fijar cods a Arreglo#vacio
    cods#agregarAlFinal: ({if ~t then}#formatear: (yo#visitar: nodo#condicional))

    cods#agregarAlFinal: subSiVerdadero#iniciar
    ParaCadaElemento: nodo#siVerdadero, procedimiento: stmt
        cods#agregarAlFinal: (subSiVerdadero#visitar: stmt)
    finprocedimiento

    cods#agregarAlFinal: {else}

    cods#agregarAlFinal: subSiFalso#iniciar
    ParaCadaElemento: nodo#siFalso, procedimiento: stmt
        cods#agregarAlFinal: (subSiFalso#visitar: stmt)
    finprocedimiento

    cods#agregarAlFinal: {end}

    devolver Unir: cods, {~%}#formatear
finmetodo

metodo CompiladorSimpleALua#visitarMientras: nodo
    variables cods, subyo

    fijar subSiVerdadero a CompiladorSimpleALua#crearSubámbitoDesdeNodo: nodo

    fijar cods a Arreglo#vacio
    cods#agregarAlFinal: ({while ~t do}#formatear: (yo#visitar: nodo#condicional))

    cods#agregarAlFinal: subyo#iniciar
    ParaCadaElemento: nodo#siVerdadero, procedimiento: stmt
        cods#agregarAlFinal: (subyo#visitar: stmt)
    finprocedimiento

    cods#agregarAlFinal: {end}

    devolver Unir: cods, {~%}#formatear
finmetodo

metodo CompiladorSimpleALua#visitarMétodo: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarAtributos: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarIdentificador: nodo
    devolver IdentificadorDe: nodo
finmetodo

metodo CompiladorSimpleALua#visitarNúmeroLiteral: nodo
    devolver nodo#valor
finmetodo

metodo CompiladorSimpleALua#visitarTextoLiteral: nodo
    devolver EscaparParaLua: nodo#valor
finmetodo

metodo CompiladorSimpleALua#enviarMensaje: objeto, mensaje, argumentos
    variables args, argsComoTexto
    fijar args a Mapear: argumentos, funcion: arg
        devolver yo#visitar: arg
    finfuncion
    si args#longitud = 0
        fijar argsComoTexto a {}
    sino
        fijar argsComoTexto a {, }#concatenar: (Unir: args, {, })
    finsi
    devolver {rt.enviarMensaje(~t, ~t~t)}#formatear: objeto, (EscaparParaLua: mensaje), argsComoTexto
finmetodo

metodo CompiladorSimpleALua#visitarLlamarProcedimiento: nodo
    devolver yo#enviarMensaje: (IdentificadorDe: nodo#proc), {llamar}, nodo#argumentos
finmetodo

metodo CompiladorSimpleALua#visitarEnviarMensaje: nodo
    devolver yo#enviarMensaje: (yo#visitar: nodo#objeto), nodo#mensaje, nodo#argumentos
finmetodo

metodo CompiladorSimpleALua#visitarOperador: nodo
    devolver {rt.enviarMensaje(~t, ~t, ~t)}#formatear:
        (yo#visitar: nodo#lhs),
        (EscaparParaLua: ({operador_~t}#formatear: nodo#op#op)),
        (yo#visitar: nodo#rhs)
finmetodo

metodo CompiladorSimpleALua#visitarNoLlamar: nodo
    variables cod
    si nodo#mensajes#longitud > 0
        fijar cod a Reducir: (yo#visitar: nodo#base), funcion: acc, mensaje
            devolver {rt.enviarMensaje(~t, ~t)}#formatear: acc, (EscaparParaLua: mensaje)
        finfuncion, (PedazoDeArreglo: nodo#mensajes, 0, -1)
        fijar cod a {rt.enviarMensaje(~t, ~t, ...)}#formatear: cod, (ÚltimoElemento: nodo#mensajes)
    sino
        fijar cod a {rt.enviarMensaje(~t, "llamar", ...)}#formatear: (yo#visitar: nodo#base)
    finsi
    devolver {function(...) return ~t end}#formatear: cod
finmetodo

metodo CompiladorSimpleALua#visitarAutoejecutar: nodo
    devolver yo#enviarMensaje: (yo#visitar: nodo#expr), {llamar}, nodo#argumentos
finmetodo

metodo CompiladorSimpleALua#visitarFunciónAnónima: nodo
    variable nrdyo, subyo
    fijar subyo a CompiladorSimpleALua#crearSubámbitoDesdeNodo: nodo
    si nodo#esMétodo
        fijar nrdyo a nodo#obtenerMetadato: LLAVE_RESOLUCIÓN_DE_NOMBRES, {nombreResueltoDeYo}
    sino
        fijar nrdyo a NULO
    finsi
    devolver subyo#expresiónDeFunción: nrdyo, nodo#parámetros, nodo#cuerpo
finmetodo

metodo CompiladorSimpleALua#visitarSonIguales: nodo
    variable igual
    fijar igual a yo#enviarMensaje: (yo#visitar: nodo#lhs), {igualA}, (Arreglo#crearCon: nodo#rhs)
    si nodo#tipoIgualdad = AST#TipoIgualdad#IGUALES
        devolver igual
    sino
        devolver {not ~t}#formatear: igual
    finsi
finmetodo

metodo CompiladorSimpleALua#visitarReferenciar: nodo
    NoImplementado
finmetodo

metodo CompiladorSimpleALua#visitarNo: nodo
    devolver {not ~t}#formatear: (yo#visitar: nodo#expresión)
finmetodo

funcion CompilarALua: ast, ámbito
    variables comp, partes, ini
    fijar comp a CompiladorSimpleALua#crear: ámbito
    fijar ini a comp#iniciar
    fijar partes a Mapear: ast, funcion: nodo
        devolver comp#visitar: nodo
    finfuncion
    devolver {~t~%~t}#formatear: ini, (Unir: partes, {~%}#formatear)
finfuncion
