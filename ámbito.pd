utilizar bepd/builtins

clase DatosDeVariable
    atributos binding, esAutoejecutable

    metodo comoTexto
finclase

metodo DatosDeVariable#inicializar: binding, esAutoejecutable
    fijar yo#binding a binding
    fijar yo#esAutoejecutable a esAutoejecutable
finmetodo

metodo DatosDeVariable#comoTexto
    devolver {(DatosDeVariable#crear: ~t, ~t)}#formatear: yo#binding, yo#esAutoejecutable
finmetodo

clase Ámbito
    metodo estatico vacío
    metodo estatico desdeArreglo: lst
    metodo estatico conValores: ...vals

    atributo ámbitoPadre

    metodo inicializar

    metodo agregar: nombre, valor
    metodo marcarComoAutoejecutable: nombre
    metodo eliminarNombre: nombre
    metodo eliminarNombreYBinding: nombre
    metodo obtenerBinding: nombre
    metodo esAutoejecutable: nombre
    metodo contiene: nombre

    metodo crearSubámbito

    metodo comoTexto

    metodo paraCadaBinding: proc
    metodo paraCadaVariable: proc

    metodo todosLosBindings
    metodo todosLosBindingsLocales
finclase

atributos Ámbito#_mapeo, Ámbito#_todosLosBindings

metodo estatico Ámbito#vacío
    devolver yo#crear
finmetodo

metodo estatico Ámbito#desdeArreglo: lst
    variable inst
    fijar inst a yo#crear
    fijar inst#_mapeo a Diccionario#desdeArreglo: lst
    devolver inst
finmetodo

metodo estatico Ámbito#conValores: ...vals
    variable inst
    fijar inst a yo#crear
    fijar inst#_mapeo a __EnviarMensaje: Diccionario, {desdeArreglo}, vals
    devolver inst
finmetodo

metodo Ámbito#inicializar
    fijar yo#_mapeo a Diccionario#vacío
    fijar yo#_todosLosBindings a Arreglo#vacio
    fijar yo#ámbitoPadre a NULO
finmetodo

metodo Ámbito#agregar: nombre, binding
    yo#_mapeo#fijarEn: nombre, (DatosDeVariable#crear: binding, FALSO)
    yo#_todosLosBindings#agregarAlFinal: binding
finmetodo

metodo Ámbito#marcarComoAutoejecutable: nombre
    variable dv
    fijar dv a yo#_mapeo#en: nombre
    fijar dv#esAutoejecutable a VERDADERO
finmetodo

metodo Ámbito#eliminarNombre: nombre
    yo#_mapeo#eliminar: nombre
finmetodo

metodo Ámbito#eliminarNombreYBinding: nombre
    variables idx, bindingDelNombre
    fijar bindingDelNombre a (yo#_mapeo#en: nombre)#binding
    yo#_mapeo#eliminar: nombre
    fijar idx a LlamarConEC: procedimiento: esc
        ParaCadaElementoConÍndice: yo#_todosLosBindings, procedimiento: binding, i
            si binding = bindingDelNombre
                %esc: i
            finsi
        finprocedimiento
        devolver NULO
    finprocedimiento
    necesitas no EsNulo: idx
    fijar yo#_todosLosBindings a EliminarElementoEnÍndice: yo#_todosLosBindings, idx
finmetodo

metodo Ámbito#_obtener: nombre
    si yo#_mapeo#contiene: nombre
        devolver yo#_mapeo#en: nombre
    sino
        si EsNulo: yo#ámbitoPadre
            __FallarConMensaje: ({El ámbito no contiene el nombre ~T}#formatear: nombre)
        sino
            devolver yo#ámbitoPadre#_obtener: nombre
        finsi
    finsi
finmetodo

metodo Ámbito#obtenerBinding: nombre
    devolver (yo#_obtener: nombre)#binding
finmetodo

metodo Ámbito#esAutoejecutable: nombre
    devolver (yo#_obtener: nombre)#esAutoejecutable
finmetodo

metodo Ámbito#contiene: nombre
    devolver yo#_mapeo#contiene: nombre
finmetodo

metodo Ámbito#crearSubámbito
    variable sub
    fijar sub a Ámbito#vacío
    fijar sub#ámbitoPadre a yo
    devolver sub
finmetodo

metodo Ámbito#comoTexto
    devolver {(Ámbito: valores = ~t, padre = ~t)}#formatear: yo#_mapeo, yo#ámbitoPadre
finmetodo

metodo Ámbito#paraCadaBinding: proc
    yo#paraCadaVariable: procedimiento: nombre, dv
        devolver %proc: nombre, dv#binding
    finprocedimiento
finmetodo

metodo Ámbito#paraCadaVariable: proc
    yo#_mapeo#paraCadaPar: proc
finmetodo

metodo Ámbito#todosLosBindingsLocales
    devolver yo#_todosLosBindings
finmetodo

metodo Ámbito#todosLosBindings
    variable todosLosBindingsPadre
    si no EsNulo: yo#ámbitoPadre
        fijar todosLosBindingsPadre a yo#ámbitoPadre#todosLosBindings
    sino
        fijar todosLosBindingsPadre a Arreglo#vacio
    finsi
    devolver Concatenar: yo#_todosLosBindings, todosLosBindingsPadre
finmetodo
