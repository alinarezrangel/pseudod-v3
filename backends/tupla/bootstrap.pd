procedimiento __ObtenerRT
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_obtener_rt}
    __Tupla#PRAGMA_IMPORT
finprocedimiento

fijar __RT a __ObtenerRT
fijar __RT#entornoBootstrap a __EACT

variables NULO, VERDADERO, FALSO
fijar NULO a %(funcion finfuncion)
fijar VERDADERO a 1 = 1
fijar FALSO a no VERDADERO


funcion __TipoDe: obj
    __Tupla#obtener: obj
    devolver __Tupla#eOBJTAG
finfuncion

funcion __SonElMismoObjeto: A, B
    devolver son identicos A y B
finfuncion

funcion __ObtenerAtributo: obj, n
    __Tupla#obtener: obj
    __Tupla#obtener: n
    devolver __Tupla#eOBJATTR
finfuncion

funcion __FijarAtributo: obj, n, val
    __Tupla#obtener: obj
    __Tupla#obtener: n
    __Tupla#obtener: val
    __Tupla#iOBJATTRSET
finfuncion

funcion __NúmeroDeAtributos: obj
    __Tupla#obtener: obj
    devolver __Tupla#eOBJSZ
finfuncion

funcion __CódigoDeObjeto: obj
    __Tupla#obtener: obj
    devolver __Tupla#eOBJ2CLZ
finfuncion

variables __TOBJ_ENTERO, __TOBJ_FLOAT, __TOBJ_MARCA_DE_PILA, __TOBJ_CLOSURE,
          __TOBJ_TEXTO, __TOBJ_OBJETO, __TOBJ_BOOLEANO, __TOBJ_NULO,
          __TOBJ_ARREGLO, __TOBJ_VOIDPTR, __TOBJ_ESPACIO_DE_NOMBRES
[ NOTA: Debe mantenerse sincronizado con la enumeración `pdcrt_tipo_de_objeto`
  en el runtime de C. ]
fijar __TOBJ_ENTERO             a 0
fijar __TOBJ_FLOAT              a 1
fijar __TOBJ_MARCA_DE_PILA      a 2
fijar __TOBJ_CLOSURE            a 3
fijar __TOBJ_TEXTO              a 4
fijar __TOBJ_OBJETO             a 5
fijar __TOBJ_BOOLEANO           a 6
fijar __TOBJ_NULO               a 7
fijar __TOBJ_ARREGLO            a 8
fijar __TOBJ_VOIDPTR            a 9
fijar __TOBJ_ESPACIO_DE_NOMBRES a 10

funcion __EsUn: obj, tipo
    devolver (__TipoDe: obj) = tipo
finfuncion


procedimiento __ParaCadaElementoConÍndice: arr, proc
    variable i
    fijar i a 0
    mientras i < arr#longitud
        %proc: (arr#en: i), i
        fijar i a i + 1
    finmientras
finprocedimiento

procedimiento __ParaCadaElemento: arr, proc
    __ParaCadaElementoConÍndice: arr, procedimiento: el, i
        devolver %proc: el
    finprocedimiento
finprocedimiento

procedimiento __FallarConMensaje: msj
    __RT#fallarConMensaje: msj
finprocedimiento

procedimiento __EnviarMensaje: obj, msj, args
    devolver __EM: obj, msj, args
finprocedimiento

variable A, B, __Impl
fijar __Impl a {Tupla (IR) Bootstrap}

funcion __CrearArreglo: ...arr
    devolver arr
finfuncion

funcion __ArregloComoTexto: arr
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_arreglo_como_texto}
    variables res, pos
    fijar res a __CrearArreglo
    fijar pos a 0
    res#agregarAlFinal: {(Arreglo#crearCon}
    __ParaCadaElemento: arr, procedimiento: el
        si pos = 0
            res#agregarAlFinal: {: }
        sino
            res#agregarAlFinal: {, }
        finsi
        res#agregarAlFinal: el#comoTexto
        fijar pos a pos + 1
    finprocedimiento
    res#agregarAlFinal: {)}
    devolver __RT#construirTexto: res
finfuncion

funcion __ClonarArreglo: arr
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_clonar_arreglo}
    variable clon
    fijar clon a __CrearArreglo
    __ParaCadaElemento: arr, procedimiento: el
        clon#agregarAlFinal: el#\clonar\
    finprocedimiento
    devolver clon
finfuncion

procedimiento __ParaCadaNúmeroBuscando: desde, hasta, cuerpo
    variable i
    fijar i a desde
    mientras i < hasta
        si %cuerpo: i
            devolver VERDADERO
        finsi
        fijar i a i + 1
    finmientras
    devolver FALSO
finprocedimiento

procedimiento __ParaCadaNúmero: desde, hasta, cuerpo
    devolver __ParaCadaNúmeroBuscando: desde, hasta, procedimiento: x %cuerpo: x devolver FALSO finprocedimiento
finprocedimiento

funcion __ArregloIgualA: yo, arr
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_arreglo_igual_a}
    si no __EsUn: arr, __TOBJ_ARREGLO
        devolver FALSO
    finsi
    si no yo#longitud = arr#longitud
        devolver FALSO
    finsi
    devolver no __ParaCadaNúmeroBuscando: 0, yo#longitud, procedimiento: i
        devolver no (yo#en: i)#igualA: (arr#en: i)
    finprocedimiento
finfuncion

funcion __ArregloDistíntoDe: yo, arr
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_arreglo_distinto_de}
    si no __EsUn: arr, __TOBJ_ARREGLO
        devolver VERDADERO
    finsi
    si no yo#longitud = arr#longitud
        devolver VERDADERO
    finsi
    devolver __ParaCadaNúmeroBuscando: 0, yo#longitud, procedimiento: i
        devolver (yo#en: i)#distíntoDe: (arr#en: i)
    finprocedimiento
finfuncion

funcion __ArregloOperadorIgual: yo, arr
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_arreglo_operador_igual}
    si no __EsUn: arr, __TOBJ_ARREGLO
        devolver FALSO
    finsi
    si no yo#longitud = arr#longitud
        devolver FALSO
    finsi
    devolver no __ParaCadaNúmeroBuscando: 0, yo#longitud, procedimiento: i
        devolver no (yo#en: i) = (arr#en: i)
    finprocedimiento
finfuncion

funcion __ArregloOperadorDistínto: yo, arr
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_arreglo_operador_distinto}
    si no __EsUn: arr, __TOBJ_ARREGLO
        devolver VERDADERO
    finsi
    si no yo#longitud = arr#longitud
        devolver VERDADERO
    finsi
    devolver __ParaCadaNúmeroBuscando: 0, yo#longitud, procedimiento: i
        devolver (yo#en: i)#\operador_no=\: (arr#en: i)
    finprocedimiento
finfuncion

funcion __ArregloMapear: yo, f
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_arreglo_mapear}
    variable res
    fijar res a __CrearArreglo
    __ParaCadaElemento: yo, procedimiento: el
        res#agregarAlFinal: (%f: el)
    finprocedimiento
    devolver res
finfuncion

variable __NL
fijar __NL a ({
})

funcion __TextoFormatear: yo, ...valores
    __Tupla#PRAGMA_CNAME: {pdcrt_frt_texto_formatear}
    variables i, res, val
    fijar i a 0
    fijar val a 0

    procedimiento consumir
        variable c
        fijar c a yo#en: i
        fijar i a i + 1
        devolver c
    finprocedimiento

    procedimiento emitir: c
        res#agregarAlFinal: c
    finprocedimiento

    procedimiento siguienteValor
        variable ret
        fijar ret a valores#en: val
        fijar val a val + 1
        devolver ret
    finprocedimiento

    fijar res a __CrearArreglo
    mientras i < yo#longitud
        variable c
        fijar c a consumir
        si c = {~}
            fijar c a consumir
            si c = {t}
                res#agregarAlFinal: siguienteValor#comoTexto
            finsi
            si c = {T}
                res#agregarAlFinal: siguienteValor
            finsi
            si c = {~}
                emitir: {~}
            finsi
            si c = {%}
                emitir: __NL
            finsi
            si c = {e}
                emitir: "}"
            finsi
            si c = {E}
                emitir: {»}
            finsi
            si c = {q}
                emitir: {"}
            finsi
            si c = {|}
                necesitas consumir = {%}
                necesitas consumir = __NL
            finsi
        sino
            emitir: c
        finsi
    finmientras

    devolver __RT#construirTexto: res
finfuncion

funcion __ObtenerCLI
    variables cli, i
    fijar cli a __CrearArreglo
    fijar i a 0
    mientras i < __RT#argc
        cli#agregarAlFinal: (__RT#argv: i)
        fijar i a i + 1
    finmientras
    devolver cli
finfuncion

variable __Argv
fijar __Argv a __ObtenerCLI

procedimiento ErrorDeMensajeNoEncontrado: msj, extra
    __FallarConMensaje: ({Mensaje ~t no encontrado: ~t}#formatear: msj, extra)
finprocedimiento

procedimiento __Escribir: X
    escribir X#comoTexto
    nl
finprocedimiento

procedimiento __Escribir': X, Y
    escribir X#comoTexto
    escribir { }
    escribir Y#comoTexto
    nl
finprocedimiento

procedimiento __Escribir'': X, Y, Z
    escribir X#comoTexto
    escribir { }
    escribir Y#comoTexto
    escribir { }
    escribir Z#comoTexto
    nl
finprocedimiento

funcion Aplicar: proc, args
    devolver __EnviarMensaje: proc, {llamar}, args
finfuncion

funcion ProcedimientoVarargs: n, proc
    devolver funcion: ...Xs
        si Xs#longitud < n
            __FallarConMensaje: ({Se esperaban al menos ~t argumentos para el procedimiento vararg, pero se recibieron ~t}#formatear: n, Xs#longitud)
        finsi
        variables inicio, resto
        fijar inicio a __CrearArreglo
        fijar resto a __CrearArreglo
        __ParaCadaElementoConÍndice: Xs, procedimiento: argumento, i
            si i < n
                inicio#agregarAlFinal: argumento
            sino
                resto#agregarAlFinal: argumento
            finsi
        finprocedimiento
        devolver %proc: ...inicio, resto
    finfuncion
finfuncion

funcion __TablaHashConCapacidad: capacidad
    variable nodos
    fijar nodos a __CrearArreglo
    __ParaCadaNúmero: 0, capacidad, procedimiento: _i
        nodos#agregarAlFinal: __CrearArreglo
    finprocedimiento

    funcion buscarEn: llave
        variables valor, encontrado, hash, colisiones
        fijar valor a NULO
        fijar encontrado a FALSO

        fijar hash a llave#hashPara: nodos#longitud
        necesitas hash >= 0
        necesitas hash < nodos#longitud
        fijar colisiones a nodos#en: hash
        fijar encontrado a __ParaCadaNúmeroBuscando: 0, colisiones#longitud, procedimiento: i
            variable par
            fijar par a colisiones#en: i
            si llave = (par#en: 0)
                fijar valor a par#en: 1
                devolver VERDADERO
            sino
                devolver FALSO
            finsi
        finprocedimiento

        devolver __CrearArreglo: encontrado, valor
    finfuncion

    procedimiento fijarEn: llave, valor
        variables hash, colisiones, existía
        fijar hash a llave#hashPara: nodos#longitud
        necesitas hash >= 0
        necesitas hash < nodos#longitud
        fijar colisiones a nodos#en: hash
        fijar existía a __ParaCadaNúmeroBuscando: 0, colisiones#longitud, procedimiento: i
            variable par
            fijar par a colisiones#en: i
            si llave = (par#en: 0)
                par#fijarEn: 1, valor
                devolver VERDADERO
            sino
                devolver FALSO
            finsi
        finprocedimiento
        si no existía
            colisiones#agregarAlFinal: (__CrearArreglo: llave, valor)
        finsi
        devolver existía
    finprocedimiento

    funcion cargaDeLaTabla
        variable res
        fijar res a 0
        __ParaCadaElemento: nodos, procedimiento: colisiones
            fijar res a res + (colisiones#longitud > 0)#escojer: 1, 0
        finprocedimiento
        devolver res
    finfuncion

    procedimiento intentaRehashear
        si cargaDeLaTabla > (nodos#longitud / 2)
            rehashear: (nodos#longitud * 2)
        finsi
    finprocedimiento

    procedimiento rehashear: nuevaCapacidad
        variable viejosNodos
        fijar viejosNodos a nodos
        fijar nodos a __CrearArreglo
        __ParaCadaNúmero: 0, nuevaCapacidad, procedimiento: _i
            nodos#agregarAlFinal: __CrearArreglo
        finprocedimiento
        __ParaCadaElemento: viejosNodos, procedimiento: colisiones
            __ParaCadaElemento: colisiones, procedimiento: par
                fijarEn: (par#en: 0), (par#en: 1)
            finprocedimiento
        finprocedimiento
    finprocedimiento

    devolver procedimiento: msj, ...args
        si msj = {buscarEn}
            necesitas args#longitud = 1
            devolver buscarEn: (args#en: 0)
        finsi
        si msj = {en}
            necesitas args#longitud = 1
            variable res
            fijar res a buscarEn: (args#en: 0)
            si no res#en: 0
                __FallarConMensaje: ({Llave ~t no encontrada en la tabla hash}#formatear: (args#en: 0))
            finsi
            devolver res#en: 1
        finsi
        si msj = {contiene}
            necesitas args#longitud = 1
            devolver (buscarEn: (args#en: 0))#en: 0
        finsi
        si msj = {buscar}
            necesitas args#longitud = 1
            devolver buscarEn: (args#en: 0)
        finsi
        si msj = {buscarONulo}
            necesitas args#longitud = 1
            variable res
            fijar res a buscarEn: (args#en: 0)
            si no res#en: 0
                devolver NULO
            sino
                devolver res#en: 1
            finsi
        finsi

        si msj = {fijarEn}
            necesitas args#longitud = 2
            si no fijarEn: (args#en: 0), (args#en: 1)
                [ Si la llave no existía, la acabamos de crear. ]
                intentaRehashear
            finsi
            devolver NULO
        finsi

        si msj = {paraCadaPar}
            necesitas args#longitud = 1
            variable f
            fijar f a args#en: 0
            __ParaCadaElemento: nodos, procedimiento: colisiones
                __ParaCadaElemento: colisiones, procedimiento: par
                    devolver %f: (par#en: 0), (par#en: 1)
                finprocedimiento
            finprocedimiento
            devolver NULO
        finsi

        si msj = {interno}
            necesitas args#longitud = 0
            devolver nodos
        finsi

        ErrorDeMensajeNoEncontrado: msj, {En la tabla hash}
    finprocedimiento#comoObjeto
finfuncion

funcion __TablaHashDesdeArreglo: arregloDePares
    variable res
    fijar res a __TablaHashConCapacidad: arregloDePares#longitud
    __ParaCadaElemento: arregloDePares, procedimiento: par
        res#fijarEn: (par#en: 0), (par#en: 1)
    finprocedimiento
    devolver res
finfuncion

funcion __ClonarObjeto: obj
    variable clon
    fijar clon a (__CódigoDeObjeto: obj)#comoObjeto
    variable i
    fijar i a 0
    mientras i < (__NúmeroDeAtributos: clon)
        __FijarAtributo: clon, i, (__ObtenerAtributo: obj, i)#\clonar\
        fijar i a i + 1
    finmientras
    devolver clon
finfuncion

funcion __CompararObjeto: A, B
    si (no (__TipoDe: A) = __TOBJ_OBJETO)
    || (no (__TipoDe: B) = __TOBJ_OBJETO)
        devolver FALSO
    finsi
    si no (__CódigoDeObjeto: A)#__códigoIgualA: (__CódigoDeObjeto: B)
        devolver FALSO
    finsi
    si no (__NúmeroDeAtributos: A) = (__NúmeroDeAtributos: B)
        devolver FALSO
    finsi
    variable i
    fijar i a 0
    mientras i < (__NúmeroDeAtributos: A)
        si no (__ObtenerAtributo: A, i) = (__ObtenerAtributo: B, i)
            devolver FALSO
        finsi
        fijar i a i + 1
    finmientras
    devolver VERDADERO
finfuncion

variable __MsjObtenerAtributos
fijar __MsjObtenerAtributos a procedimiento: msj, ...args
    variable yo
    fijar yo a __ESUP

    si (msj = {igualA}) || (msj = {operador_=})
        necesitas args#longitud = 1
        devolver son identicos yo y (args#en: 0)
    finsi
    si msj = {clonar}
        necesitas args#longitud = 0
        devolver yo
    finsi
    si msj = {comoTexto}
        necesitas args#longitud = 0
        devolver {__MsjObtenerAtributos}
    finsi

    ErrorDeMensajeNoEncontrado: msj, {En la instancia del mensaje especial __MsjObtenerAtributos}
finprocedimiento#comoObjeto

variable __MsjObtenerClase
fijar __MsjObtenerClase a procedimiento: msj, ...args
    variable yo
    fijar yo a __ESUP

    si (msj = {igualA}) || (msj = {operador_=})
        necesitas args#longitud = 1
        devolver son identicos yo y (args#en: 0)
    finsi
    si msj = {clonar}
        necesitas args#longitud = 0
        devolver yo
    finsi
    si msj = {comoTexto}
        necesitas args#longitud = 0
        devolver {__MsjObtenerClase}
    finsi

    ErrorDeMensajeNoEncontrado: msj, {En la instancia del mensaje especial __MsjObtenerAtributos}
finprocedimiento#comoObjeto

funcion __CrearInstanciaDeClase: cls, instReal, métodos, atrs
    funcion obtenerMétodo: msj
        variable idc
        fijar idc a métodos#longitud - 1
        mientras idc >= 0
            variable método
            fijar método a métodos#en: idc
            fijar idc a idc - 1

            si (método#en: 0) = msj
                devolver método#en: 1
            finsi
        finmientras
        devolver NULO
    finfuncion

    devolver procedimiento: msj, ...args
        variables instFalsa, instProxy
        fijar instFalsa a __ESUP

        si son identicas instReal y NULO
            fijar instProxy a instFalsa
        sino
            fijar instProxy a instReal
        finsi

        si msj = __MsjObtenerAtributos
            necesitas args#longitud = 0
            devolver atrs
        finsi

        si msj = __MsjObtenerClase
            necesitas args#longitud = 0
            devolver cls
        finsi

        variable proc
        fijar proc a obtenerMétodo: msj
        si no son identicos proc y NULO
            devolver %proc: instProxy, ...args
        finsi

        si (msj = {igualA}) || (msj = {operador_=})
            necesitas args#longitud = 1
            devolver __CompararObjeto: instProxy, (args#en: 0)
        finsi
        si msj = {clonar}
            necesitas args#longitud = 0
            variable atrsClonados
            fijar atrsClonados a __CrearArreglo
            __ParaCadaElemento: atrs, procedimiento: el
                atrsClonados#agregarAlFinal: el#\clonar\
            finprocedimiento
            devolver __CrearInstanciaDeClase: cls, instReal, métodos, atrsClonados
        finsi

        fijar proc a obtenerMétodo: {metodoNoEncontrado}
        si no son identicos proc y NULO
            devolver %proc: instProxy, msj, args
        finsi

        ErrorDeMensajeNoEncontrado: msj, ({En una instancia de la clase ~t}#formatear: cls#nombre)
    finprocedimiento#comoObjeto
finfuncion

funcion __CrearMétodosDeInstancia: métodosDeInstancia, atributosDeInstancia, atrs
    variable métodos
    fijar métodos a __CrearArreglo

    __ParaCadaElemento: métodosDeInstancia, procedimiento: el
        métodos#agregarAlFinal: (__CrearArreglo: (el#en: 0), (el#en: 1))
    finprocedimiento

    __ParaCadaElementoConÍndice: atributosDeInstancia, procedimiento: atr, i
        métodos#agregarAlFinal: (__CrearArreglo: atr, metodo
            devolver (__EnviarMensaje: yo, __MsjObtenerAtributos, __CrearArreglo)#en: i
        finmetodo)
        métodos#agregarAlFinal: (__CrearArreglo: ({fijar_}#concatenar: atr), metodo: valor
            (__EnviarMensaje: yo, __MsjObtenerAtributos, __CrearArreglo)#fijarEn: i, valor
        finmetodo)
    finprocedimiento

    devolver métodos
finfuncion

procedimiento __CrearClase: nombre
    variables métodosDeInstancia, atributosDeInstancia, claseBase, métodosEstaticos
    fijar métodosDeInstancia a __CrearArreglo:
        (__CrearArreglo: {inicializar}, metodo finmetodo)
    fijar atributosDeInstancia a __CrearArreglo
    fijar claseBase a NULO
    fijar métodosEstaticos a __CrearArreglo

    funcion obtenerMétodoEstático: msj
        variable idc
        fijar idc a métodosEstaticos#longitud - 1
        mientras idc >= 0
            variable métodoEstatico
            fijar métodoEstatico a métodosEstaticos#en: idc
            fijar idc a idc - 1

            si (métodoEstatico#en: 0) = msj
                devolver métodoEstatico#en: 1
            finsi
        finmientras
        devolver NULO
    finfuncion

    devolver procedimiento: msj, ...args
        variable yo
        fijar yo a __ESUP

        variable met
        fijar met a obtenerMétodoEstático: msj
        si no son identicos met y NULO
            devolver %met: yo, ...args
        finsi

        si (msj = {igualA}) || (msj = {operador_=})
            necesitas args#longitud = 1
            devolver son identicos yo y (args#en: 0)
        finsi

        si msj = {clonar}
            necesitas args#longitud = 0
            devolver yo
        finsi

        si msj = {comoTexto}
            necesitas args#longitud = 0
            devolver {Clase ~t}#formatear: yo#nombre
        finsi

        si msj = {__métodosDeInstancia}
            necesitas args#longitud = 0
            devolver métodosDeInstancia
        finsi

        si msj = {__atributosDeInstancia}
            necesitas args#longitud = 0
            devolver atributosDeInstancia
        finsi

        si msj = {__métodosEstaticos}
            necesitas args#longitud = 0
            devolver métodosEstaticos
        finsi

        si msj = {fijar___métodosDeInstancia}
            necesitas args#longitud = 1
            fijar métodosDeInstancia a args#en: 0
            devolver NULO
        finsi

        si msj = {fijar___atributosDeInstancia}
            necesitas args#longitud = 1
            fijar atributosDeInstancia a args#en: 0
            devolver NULO
        finsi

        si msj = {fijar___métodosEstaticos}
            necesitas args#longitud = 1
            fijar métodosEstaticos a args#en: 0
            devolver NULO
        finsi

        si msj = {nombre}
            necesitas args#longitud = 0
            devolver nombre
        finsi

        si msj = {claseBase}
            necesitas args#longitud = 0
            devolver claseBase
        finsi

        si msj = {fijar_nombre}
            necesitas args#longitud = 1
            fijar nombre a args#en: 0
            devolver NULO
        finsi

        si msj = {fijar_claseBase}
            necesitas args#longitud = 1
            fijar claseBase a args#en: 0
            devolver NULO
        finsi

        si (msj = {agregarMetodo}) || (msj = {agregarMétodo})
            necesitas args#longitud = 2
            variables nombre, método
            fijar nombre a args#en: 0
            fijar método a args#en: 1
            métodosDeInstancia#agregarAlFinal: (__CrearArreglo: nombre, método)
            devolver NULO
        finsi

        si msj = {agregarAtributo}
            necesitas args#longitud = 1
            variable nombre
            fijar nombre a args#en: 0
            atributosDeInstancia#agregarAlFinal: nombre
            devolver NULO
        finsi

        si msj = {agregarMétodoEstático}
            necesitas args#longitud = 2
            métodosEstaticos#agregarAlFinal: (__CrearArreglo: (args#en: 0), (args#en: 1))
            devolver NULO
        finsi

        si msj = {_crearConYo}
            necesitas args#longitud = 1
            variables instReal, atrs, métodos
            fijar instReal a args#en: 0
            fijar atrs a __EnviarMensaje: instReal, __MsjObtenerAtributos, __CrearArreglo
            fijar métodos a __CrearMétodosDeInstancia: métodosDeInstancia, atributosDeInstancia, atrs
            devolver __CrearInstanciaDeClase: yo, instReal, métodos, atrs
        finsi

        si msj = {_crear}
            necesitas args#longitud = 0
            variables atrs, métodos
            fijar atrs a atributosDeInstancia#mapear: funcion: v devolver NULO finfuncion
            fijar métodos a __CrearMétodosDeInstancia: métodosDeInstancia, atributosDeInstancia, atrs
            devolver __CrearInstanciaDeClase: yo, NULO, métodos, atrs
        finsi

        si msj = {crear}
            variable inst
            fijar inst a yo#_crear
            inst#inicializar: ...args
            devolver inst
        finsi

        si msj = {subclase}
            necesitas args#longitud = 0
            variable subclase
            fijar subclase a __CrearClase: {<clase anónima>}
            fijar subclase#claseBase a yo
            fijar subclase#__atributosDeInstancia a atributosDeInstancia#mapear: funcion: el devolver el finfuncion
            fijar subclase#__métodosDeInstancia a métodosDeInstancia#mapear: funcion: el devolver el finfuncion
            fijar subclase#__métodosEstaticos a métodosEstaticos#mapear: funcion: el devolver el finfuncion
            devolver subclase
        finsi

        fijar met a obtenerMétodoEstático: {metodoNoEncontrado}
        si no son identicos met y NULO
            devolver %met: yo, msj, args
        finsi

        ErrorDeMensajeNoEncontrado: msj, ({En la clase ~t}#formatear: nombre)
    finprocedimiento#comoObjeto
finprocedimiento


variable Objeto
fijar Objeto a __CrearClase: {Objeto}
fijar __RT#objeto a Objeto


clase Numero hereda Objeto
    metodo estatico crear
finclase

metodo estatico Numero#crear
    devolver 0
finmetodo


clase Procedimiento hereda Objeto
    metodo estatico crear
finclase

metodo estatico Procedimiento#crear
    __FallarConMensaje: {No se puede crear un procedimiento con #crear}
finmetodo


clase Referencia hereda Objeto
    metodo estatico crear
finclase

metodo estatico Referencia#crear
    __FallarConMensaje: {No se puede crear una referencia con #crear}
finmetodo


clase TipoNulo hereda Objeto
    metodo estatico crear
finclase

metodo estatico TipoNulo#crear
    devolver NULO
finmetodo


clase Texto hereda Objeto
    metodo estatico crear
    metodo estatico vacío
finclase

metodo estatico Texto#vacío
    devolver {}
finmetodo

metodo estatico Texto#crear
    devolver {}
finmetodo


clase Boole hereda Objeto
    metodo estatico crear
    metodo estatico verdadero
    metodo estatico falso
finclase

metodo estatico Boole#crear
    devolver VERDADERO
finmetodo

metodo estatico Boole#verdadero
    devolver VERDADERO
finmetodo

metodo estatico Boole#falso
    devolver FALSO
finmetodo

variable Booleano
fijar Booleano a Boole


funcion __EspacioDeNombres: ...pares
    __FallarConMensaje: {TODO}
    devolver procedimiento: msj, ...args
    finprocedimiento#comoObjeto
finfuncion


clase EspacioDeNombres hereda Objeto
    metodo estatico crear
    metodo estatico vacio
    metodo estatico vacío
finclase

metodo estatico EspacioDeNombres#crear
    devolver __EspacioDeNombres
finmetodo

metodo estatico EspacioDeNombres#vacio
    devolver __EspacioDeNombres
finmetodo

metodo estatico EspacioDeNombres#vacío
    devolver __EspacioDeNombres
finmetodo


clase Arreglo hereda Objeto
    metodo estatico crear
    metodo estatico vacio
    metodo estatico vacío
    metodo estatico crearCon
    metodo estatico conValores
finclase

metodo estatico Arreglo#crear
    devolver __CrearArreglo
finmetodo

metodo estatico Arreglo#vacio
    devolver __CrearArreglo
finmetodo

metodo estatico Arreglo#vacío
    devolver __CrearArreglo
finmetodo

metodo estatico Arreglo#crearCon: ...valores
    devolver __CrearArreglo: ...valores
finmetodo

metodo estatico Arreglo#conValores: ...valores
    devolver __CrearArreglo: ...valores
finmetodo


clase PunteroExterno hereda Objeto
    metodo estatico crear
finclase

metodo estatico PunteroExterno#crear
    __FallarConMensaje: {No se puede crear un puntero externo manualmente}
finmetodo


funcion TipoDe: objeto
    si (__EsUn: objeto, __TOBJ_ENTERO) || (__EsUn: objeto, __TOBJ_FLOAT)
        devolver Numero
    finsi
    si __EsUn: objeto, __TOBJ_BOOLEANO
        devolver Boole
    finsi
    si __EsUn: objeto, __TOBJ_NULO
        devolver TipoNulo
    finsi
    si __EsUn: objeto, __TOBJ_CLOSURE
        devolver Procedimiento
    finsi
    si __EsUn: objeto, __TOBJ_ESPACIO_DE_NOMBRES
        devolver EspacioDeNombres
    finsi
    si __EsUn: objeto, __TOBJ_ARREGLO
        devolver Arreglo
    finsi
    si __EsUn: objeto, __TOBJ_TEXTO
        devolver Texto
    finsi
    si __EsUn: objeto, __TOBJ_VOIDPTR
        devolver PunteroExterno
    finsi
    si __EsUn: objeto, __TOBJ_MARCA_DE_PILA
        __FallarConMensaje: {Error interno del runtime: marca de pila detectada en TipoDe.
Este error no es en tu programa: por favor, repórtalo como un bug.}
    finsi
    si __EsUn: objeto, __TOBJ_OBJETO
        devolver __EnviarMensaje: objeto, __MsjObtenerClase, __CrearArreglo
    finsi

    __FallarConMensaje: {Error interno del runtime: TipoDe recibió un objeto de dudosa procedencia.
Este error no es en tu programa: por favor, repórtalo como un bug.}
finfuncion

variable __ByteEof
fijar __ByteEof a -1

procedimiento __LeerCaracter
    devolver __RT#leerCaracter
finprocedimiento

funcion __ByteATexto: byte
    si (byte < 0) || (byte > 255)
        __FallarConMensaje: ({__ByteATexto: ~t debería ser un byte (un entero entre 0 y 255)}#formatear: byte)
    finsi
    devolver byte#comoByteEnTexto
finfuncion

funcion __TextoAByte: texto
    si no (texto#longitud = 1)
        __FallarConMensaje: ({__TextoAByte: ~t debería ser un texto de un solo byte}#formatear: texto)
    finsi
    devolver texto#byteEn: 0
finfuncion

procedimiento __AbrirArchivo: nombre, modo
    devolver __RT#abrirArchivo: nombre, modo
finprocedimiento

funcion __Capturar: cuerpo
    __FallarConMensaje: {__Capturar aún no está implementado}
finfuncion
