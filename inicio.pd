utilizar bepd/builtins
utilizar bepd/x/enum
utilizar bepd/utilidades/texto
utilizar bepd/x/puerto como Puerto
utilizar bepd/x/puerto/deArchivo como PDA
utilizar bepd/x/sistemaDeArchivos/archivo como Archivos
utilizar tokenizador como Tokenizador
utilizar parser como Parser
utilizar compilador como Compilador
utilizar resoluciónDeNombres como RN
utilizar backends/lua como LuaBack
utilizar módulos como Módulos


variables MENSAJE_DE_AYUDA, VERSIÓN, NOMBRE_IMPLEMENTACIÓN, RUTA_BEPD_PREDETERMINADA
fijar VERSIÓN a {3.0.0-alpha.1}
fijar NOMBRE_IMPLEMENTACIÓN a {PseudoD (bootstrap)}
fijar RUTA_BEPD_PREDETERMINADA a {/opt/pseudod/bepd/bepd/}

fijar MENSAJE_DE_AYUDA a {PseudoD versión ~t
Uso:  pseudod OPCIONES... (<archivo> | -c <programa>) [-b <ruta>]
      pseudod OPCIONES... [-i]
      pseudod (-v | --version)
      pseudod (-h | --help | -a | --ayuda | -?)

Opciones:

 <archivo>                          : Ejecuta el archivo <archivo>.
 -c <programa>                      : Ejecuta <programa> en vez de <archivo>.
 -b <ruta>                          : Fija <ruta> a la ruta a la BEPD.
 -B <ruta>                          : Igual a `-b <ruta>`. Obsoleto, existe
                                      por compatibilidad.
 -X <experimento>                   : Activa el experimento <experimento>.
 -v | --version                     : Muestra el nombre del intérprete y
                                      la versión.
 --solo-version                     : Solamente muestra la versión del
                                      intérprete.
 -h | --help | -a | --ayuda | -?    : Muestra este mensaje de ayuda y termina.
 -i                                 : Inicia el REPL. Equivalente a llamar
                                      `pseudod` sin <archivo> ni `-c`.
 -o <archivo> | --salida <archivo>  : Escribe el programa compilado a
                                      <archivo>.
 --sin-mensajes                     : No escribe los mensajes del compilador.
 -l <ruta>                          : Ignorado, existe por compatibilidad.
 --guardar-db <archivo>             : Guarda la base de datos luego de
                                      compilar en <archivo>
 --cargar-db <archivo>              : Carga la base de datos <archivo> antes de
                                      compilar.

Si la opción `-b` no es especificada, la ruta a la BEPD se extráe de:

  1. La variable de entorno `$RUTA_PSEUDOD`, o si no exíste:
  2. La variable de entorno `$RUTA_PSEUDOD_BEPD`, o si no exíste:
  3. El directorio `~t`.

El sistema de experimentos te permite probar características experimentales o
en etapa de prueba. Actualmente están implementados los siguientes
experimentos:

 -X v3x  : No hace nada.
 -X v3   : Alias de `-X v3x`.

Los siguientes parámetros pueden ser inspeccionados:

 -I ruta-bepd  : Escribe la ruta a la BEPD utilizada.
 -I rutas-bib  : Escribe las rutas a las bibliotecas. Una por línea.
}#formatear: VERSIÓN, RUTA_BEPD_PREDETERMINADA

variable MostrarVersión
fijar MostrarVersión a Enum: {MostrarVersión}, {VERSIÓN_Y_NOMBRE}, {SOLO_VERSIÓN}, {NO}

clase ContextoDeCLI
    atributos archivoAEjecutar,
              programaAEjecutar,
              mostrarAyuda,
              mostrarVersión,
              rutaDeLaBEPD,
              interactivo,
              archivoDeSalida,
              mostrarMensajes,
              guardaBaseDeDatos,
              cargaBasesDeDatos

    metodo comoTexto

    metodo mostrarMensaje: texto
finclase

metodo ContextoDeCLI#inicializar
    fijar yo#archivoAEjecutar a NULO
    fijar yo#programaAEjecutar a NULO
    fijar yo#mostrarAyuda a FALSO
    fijar yo#mostrarVersión a MostrarVersión#NO
    fijar yo#rutaDeLaBEPD a NULO
    fijar yo#interactivo a FALSO
    fijar yo#archivoDeSalida a NULO
    fijar yo#mostrarMensajes a VERDADERO
    fijar yo#guardaBaseDeDatos a NULO
    fijar yo#cargaBasesDeDatos a Arreglo#vacio
finmetodo

metodo ContextoDeCLI#comoTexto
    devolver {(ContextoDeCLI con archivoAEjecutar a ~t, programaAEjecutar ~t, mostrarAyuda ~t, mostrarVersión ~t, rutaDeLaBEPD ~t, interactivo ~t, archivoDeSalida ~t, mostrarMensajes ~t, guardaBaseDeDatos ~t, cargaBasesDeDatos ~t)}#formatear: yo#archivoAEjecutar, yo#programaAEjecutar, yo#mostrarAyuda, yo#mostrarVersión, yo#rutaDeLaBEPD, yo#interactivo, yo#archivoDeSalida, yo#mostrarMensajes, yo#guardaBaseDeDatos, yo#cargaBasesDeDatos
finmetodo

metodo ContextoDeCLI#mostrarMensaje: texto
    si yo#mostrarMensajes
        Escribir: texto
    finsi
finmetodo

funcion ObtenerArchivoDesdeCLI: argumentoDelCLI
    si argumentoDelCLI = {-}
        devolver {/dev/stdin}
    sino
        devolver argumentoDelCLI
    finsi
finfuncion

procedimiento ParsearOpción: ctx, arg, resto
    si Contiene: (Arreglo#crearCon: {-h}, {--help}, {-a}, {--ayuda}, {-?}), arg
        fijar ctx#mostrarAyuda a VERDADERO
        devolver 0
    finsi
    si Contiene: (Arreglo#crearCon: {-v}, {--version}), arg
        fijar ctx#mostrarVersión a MostrarVersión#VERSIÓN_Y_NOMBRE
        devolver 0
    finsi
    si arg = {--solo-version}
        fijar ctx#mostrarVersión a MostrarVersión#SOLO_VERSIÓN
        devolver 0
    finsi
    si arg = {-l}
        [ Especifica la ruta al NEA. El NEA ya no es utilizado en PseudoD v3 ]
        devolver 1
    finsi
    si (arg = {-b}) || (arg = {-B})
        necesitas resto#longitud > 0
        fijar ctx#rutaDeLaBEPD a ObtenerArchivoDesdeCLI: (resto#en: 0)
        devolver 1
    finsi
    si arg = {-i}
        fijar ctx#interactivo a VERDADERO
        devolver 0
    finsi
    si arg = {-c}
        necesitas EsNulo: ctx#programaAEjecutar
        necesitas resto#longitud > 0
        fijar ctx#programaAEjecutar a resto#en: 0
        devolver 1
    finsi
    si (arg = {-o}) || (arg = {--salida})
        necesitas EsNulo: ctx#archivoDeSalida
        necesitas resto#longitud > 0
        fijar ctx#archivoDeSalida a ObtenerArchivoDesdeCLI: (resto#en: 0)
        devolver 1
    finsi
    si arg = {--sin-mensajes}
        fijar ctx#mostrarMensajes a FALSO
        devolver 0
    finsi
    si arg = {--guardar-db}
        necesitas EsNulo: ctx#guardaBaseDeDatos
        necesitas resto#longitud > 0
        fijar ctx#guardaBaseDeDatos a ObtenerArchivoDesdeCLI: (resto#en: 0)
        devolver 1
    finsi
    si arg = {--cargar-db}
        necesitas resto#longitud > 0
        ctx#cargaBasesDeDatos#agregarAlFinal: (ObtenerArchivoDesdeCLI: (resto#en: 0))
        devolver 1
    finsi
    devolver NULO
finprocedimiento

procedimiento ParsearPosicionales: ctx, posicionales
    necesitas posicionales#longitud =< 1
    si posicionales#longitud = 1
        fijar ctx#archivoAEjecutar a ObtenerArchivoDesdeCLI: (posicionales#en: 0)
    finsi
finprocedimiento

procedimiento ParsearCLI: cli
    variables ctx, i, posicionales
    fijar ctx a ContextoDeCLI#crear
    fijar i a 0
    fijar posicionales a Arreglo#vacio
    mientras i < cli#longitud
        variables arg, consumidos
        fijar arg a cli#en: i
        si arg = {--}
            [ `--` termina las opciones: Luego de `--` todos los parámetros
              del cli son posicionales, incluso los que comienzan con `-`. ]
            fijar posicionales a Concatenar: posicionales, (PedazoDeArreglo: cli, (i + 1), -1)
            fijar i a cli#longitud  [ <== Termina el bucle ]
        sino
            fijar consumidos a ParsearOpción: ctx, arg, (PedazoDeArreglo: cli, (i + 1), -1)
            si EsNulo: consumidos
                necesitas (arg = {-}) || (no ((arg#en: 0) = {-}))
                posicionales#agregarAlFinal: arg
                fijar i a i + 1
            sino
                fijar i a i + 1 + consumidos
            finsi
        finsi
    finmientras
    ParsearPosicionales: ctx, posicionales
    devolver ctx
finprocedimiento

funcion LlaveDeMóduloDesdeArchivoDelCLI: archivo
    variables archivoSinExtensión, extensión, partes
    fijar partes a Partir: archivo, {.}
    fijar archivoSinExtensión a Unir: (PedazoDeArreglo: partes, 0, -2), {.}
    fijar extensión a ÚltimoElemento: partes
    devolver Módulos#LlaveDeMódulo#crear: {.}, archivoSinExtensión, extensión
finfuncion

procedimiento Compilar: ctx, programa
    ctx#mostrarMensaje: {-- Inicio}
    variables comp, db, conf, llave, módulo, compilado

    ctx#mostrarMensaje: {-- Inicializando la base de datos de módulos...}
    fijar conf a Módulos#ConfiguraciónGlobal#predeterminado
    fijar db a Módulos#BaseDeDatos#conConfiguración: conf
    ParaCadaElemento: ctx#cargaBasesDeDatos, procedimiento: nombreDelArchivo
        ctx#mostrarMensaje: ({Cargando ~t}#formatear: nombreDelArchivo)
        variable arch
        fijar arch a PDA#PuertoDeArchivoDeLectura#abrir: nombreDelArchivo
        db#cargarMódulos: arch
        arch#cerrar
    finprocedimiento

    ctx#mostrarMensaje: {-- Inicializando el compilador a Lua}
    fijar comp a LuaBack#CompiladorALua#crear: db

    ctx#mostrarMensaje: {-- Compilando el programa...}
    si no EsNulo: ctx#archivoAEjecutar
        fijar llave a LlaveDeMóduloDesdeArchivoDelCLI: ctx#archivoAEjecutar
    sino
        fijar llave a Módulos#LlaveDeMódulo#crear: {.}, {<stdin>}, {pd}
    finsi
    fijar módulo a comp#compilarTexto: llave, programa
    db#agregarMódulo: módulo

    ctx#mostrarMensaje: {-- Final...}
    fijar compilado a comp#compilarTodo: llave
    si no EsNulo: ctx#archivoDeSalida
        ctx#mostrarMensaje: ({-- Guardando en ~t}#formatear: ctx#archivoDeSalida)
        Archivos#EscribirArchivo: ctx#archivoDeSalida, (compilado#concatenar: {~%}#formatear)
    sino
        Escribir: compilado
    finsi

    si no EsNulo: ctx#guardaBaseDeDatos
        ctx#mostrarMensaje: ({-- Guardando base de datos en ~t}#formatear: ctx#guardaBaseDeDatos)
        variable arch
        fijar arch a PDA#PuertoDeArchivoDeEscritura#abrir: ctx#guardaBaseDeDatos
        db#guardarMódulos: arch
        arch#cerrar
    finsi
finprocedimiento

procedimiento CompilarArchivo: ctx, nombreDelArchivo
    Compilar: ctx, (Archivos#LeerArchivo: nombreDelArchivo)
finprocedimiento

procedimiento IniciarREPL: ctx
    ctx#mostrarMensaje: {-- REPL del compilador}
    ctx#mostrarMensaje: {-- TODO}
    Compilar: ctx, {

utilizar bepd/builtins

Escribir: "Hola Mundo"

}
finprocedimiento

procedimiento Inicio: ctxcli
    si (EsNulo: ctxcli#archivoAEjecutar) && (EsNulo: ctxcli#programaAEjecutar)
        fijar ctxcli#interactivo a VERDADERO
    finsi

    si no EsNulo: ctxcli#archivoAEjecutar
        CompilarArchivo: ctxcli, ctxcli#archivoAEjecutar
        devolver NULO
    finsi
    si no EsNulo: ctxcli#programaAEjecutar
        Compilar: ctxcli, ctxcli#programaAEjecutar
        devolver NULO
    finsi
    si ctxcli#interactivo
        IniciarREPL: ctxcli
        devolver NULO
    finsi

    Escribir: ctxcli
finprocedimiento

variable ctxcli
fijar ctxcli a ParsearCLI: __Argv
si ctxcli#mostrarAyuda
    escribir MENSAJE_DE_AYUDA
sino
    si ctxcli#mostrarVersión = MostrarVersión#VERSIÓN_Y_NOMBRE
        Escribir: ({~t ~t}#formatear: NOMBRE_IMPLEMENTACIÓN, VERSIÓN)
    sino
        si ctxcli#mostrarVersión = MostrarVersión#SOLO_VERSIÓN
            Escribir: VERSIÓN
        sino
            Inicio: ctxcli
        finsi
    finsi
finsi
