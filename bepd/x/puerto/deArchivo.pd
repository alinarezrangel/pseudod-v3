utilizar bepd/builtins
utilizar bepd/x/puerto como Puerto

[DOCUMENTA
@brief{Puerto de lectura que lee un archivo.}

Provee la misma interfáz que @pd{Puerto#PuertoDeLectura}.
DOCUMENTA]
clase PuertoDeArchivoDeLectura hereda Puerto#Puerto [ implementa Puerto#PuertoDeLectura ]
    [DOCUMENTA
    @brief{Archivo del que se lee.}
    DOCUMENTA]
    atributo archivo

    [DOCUMENTA
    @brief{Crea y devuelve una instancia que lee desde el archivo especificado.}

    Es muy importante no leer ni desleer carácteres de @pd{archivo} mientras
    este puerto siga abierto.
    DOCUMENTA]
    metodo estatico desdeArchivo: archivo

    [DOCUMENTA
    @brief{Crea y devuelve una instancia que lee desde el archivo en
    @pd{nombre}.}

    @pd{nombre} es abierto en modo de lectura y modo binario.
    DOCUMENTA]
    metodo estatico abrir: nombre

    metodo leerCarácter
    metodo desleerCarácter

    [DOCUMENTA
    @brief{Cierra el archivo de este puerto.}
    DOCUMENTA]
    metodo cerrar
finclase

atributo PuertoDeArchivoDeLectura#buffer, PuertoDeArchivoDeLectura#últimoLeído

metodo estatico PuertoDeArchivoDeLectura#desdeArchivo: archivo
    devolver yo#crear: archivo
finmetodo

metodo estatico PuertoDeArchivoDeLectura#abrir: nombre
    [ modo 10 = lectura, binario, no truncar. ]
    devolver yo#crear: (__AbrirArchivo: nombre, 10)
finmetodo

metodo PuertoDeArchivoDeLectura#inicializar: archivo
    fijar yo#archivo a archivo
    fijar yo#buffer a NULO
    fijar yo#últimoLeído a NULO
finmetodo

metodo PuertoDeArchivoDeLectura#_leerCarácter
    necesitas yo#archivo#estaAbierto
    necesitas no yo#archivo#error
    variable byte, res
    fijar byte a yo#archivo#leerByte
    si byte = __ByteEof
        fijar res a Puerto#EOF
    sino
        fijar res a __ByteATexto: byte
    finsi
    fijar yo#últimoLeído a res
    devolver res
finmetodo

metodo PuertoDeArchivoDeLectura#leerCarácter
    si EsNulo: yo#buffer
        devolver yo#_leerCarácter
    sino
        variable res
        fijar res a yo#buffer
        fijar yo#buffer a NULO
        devolver res
    finsi
finmetodo

metodo PuertoDeArchivoDeLectura#desleerCarácter
    necesitas EsNulo: yo#buffer
    necesitas no EsNulo: yo#últimoLeído
    fijar yo#buffer a yo#últimoLeído
    devolver yo#últimoLeído
finmetodo

metodo PuertoDeArchivoDeLectura#cerrar
    yo#archivo#cerrar
finmetodo


[DOCUMENTA
@brief{Un puerto de escritura que escribe a un archivo.}

Provee la misma interfáz que @pd{Puerto#PuertoDeEscritura}.
DOCUMENTA]
clase PuertoDeArchivoDeEscritura hereda Puerto#Puerto [ implementa Puerto#PuertoDeEscritura ]
    [DOCUMENTA
    @brief{Archivo al que se escribe.}
    DOCUMENTA]
    atributo archivo

    [DOCUMENTA
    @brief{Crea y devuelve una instancia que escribe al archivo especificado.}

    Es posible escribir a @pd{archivo} mientras esta instancia siga abierta.
    DOCUMENTA]
    metodo estatico desdeArchivo: archivo

    [DOCUMENTA
    @brief{Crea y devuelve una instancia que escribe al archivo en @pd{nombre}.}

    @pd{nombre} es abierto en modo de escritura y modo binario.
    DOCUMENTA]
    metodo estatico abrir: nombre

    metodo escribirTexto: texto

    [DOCUMENTA
    @brief{Cierra el archivo del puerto.}
    DOCUMENTA]
    metodo cerrar
finclase

metodo estatico PuertoDeArchivoDeEscritura#desdeArchivo: archivo
    devolver yo#crear: archivo
finmetodo

metodo estatico PuertoDeArchivoDeEscritura#abrir: nombre
    [ modo 11 = escritura, binario, no-truncar. ]
    [ NOTA: Debido a un bug, abrir un archivo como escritura siempre lo abre
      truncado, así que a pesar de que el bit de truncar no está fijo, en
      realidad esto si trunca el archivo. ]
    devolver yo#crear: (__AbrirArchivo: nombre, 11)
finmetodo

metodo PuertoDeArchivoDeEscritura#inicializar: archivo
    fijar yo#archivo a archivo
finmetodo

metodo PuertoDeArchivoDeEscritura#escribirTexto: texto
    si __Impl = {Lua Bootstrap}
        [ Este método aún no está estabilizado, por eso esta bajo el "guard"
          de __Impl ]
        yo#archivo#escribirTexto: texto
    sino
        ParaCadaElemento: texto, procedimiento: carácter
            variable byte
            fijar byte a __TextoAByte: carácter
            yo#archivo#escribirByte: byte
        finprocedimiento
    finsi
finmetodo

metodo PuertoDeArchivoDeEscritura#cerrar
    yo#archivo#cerrar
finmetodo
