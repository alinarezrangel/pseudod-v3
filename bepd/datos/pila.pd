utilizar bepd/builtinsImpl

[DOCUMENTA
@brief{Una lista de elementos FILO.}

Una pila mantiene una lista de elementos de forma que solo se pueden agregar y
remover elementos en la parte superior de la pila.
DOCUMENTA]
clase Pila
    [DOCUMENTA
    @brief{Construye una pila vacía.}
    DOCUMENTA]
    metodo estatico vacía

    [DOCUMENTA
    @brief{Construye una pila con los elementos del arreglo @pd{arr}.}

    Los primeros elementos del arreglo quedarán en la base de la pila, mientras
    que los últimos elementos quedarán en la cima.
    DOCUMENTA]
    metodo estatico desdeArreglo: arr

    [DOCUMENTA
    @brief{Construye una pila con los elementos dados.}

    Tal como con @pd{#desdeArreglo}, los primeros elementos del arreglo
    quedarán en la base de la pila, mientras que los últimos elementos quedarán
    en la cima.
    DOCUMENTA]
    metodo estatico conValores: ...valores

    [DOCUMENTA
    @brief{Convierte esta pila a un @pd{Texto}.}
    DOCUMENTA]
    metodo comoTexto

    [DOCUMENTA
    @brief{Devuelve la cantidad de elementos en la pila.}
    DOCUMENTA]
    metodo longitud

    [DOCUMENTA
    @brief{Devuelve @pd{VERDADERO} si la pila está vacía, @pd{FALSO} de
    lo contrario.}
    DOCUMENTA]
    metodo estáVacía

    [DOCUMENTA
    @brief{Apila un nuevo valor en la pila.}

    Este valor ahora estará en la parte superior.
    DOCUMENTA]
    metodo apilar: valor

    [DOCUMENTA
    @brief{Desapila el último valor apilado y lo devuelve.}
    DOCUMENTA]
    metodo desapilar

    [DOCUMENTA
    @brief{Devuelve el último valor apilado.}

    A diferencia de @pd{Pila#desapilar} este método no desapila el valor.
    DOCUMENTA]
    metodo últimoValorApilado

    [DOCUMENTA
    @brief{Vacía la pila, desapilando todos los valores hasta que esté vacía.}
    DOCUMENTA]
    metodo vaciar
finclase

atributo Pila#_elementos

metodo estatico Pila#vacía
    variable inst
    fijar inst a yo#_crear
    fijar inst#_elementos a Arreglo#vacio
    devolver inst
finmetodo

metodo estatico Pila#conValores: ...valores
    devolver yo#desdeArreglo: valores
finmetodo

metodo estatico Pila#desdeArreglo: arr
    variable inst
    fijar inst a yo#vacía
    ParaCadaElemento: arr, procedimiento: el
        inst#apilar: el
    finprocedimiento
    devolver inst
finmetodo

metodo Pila#comoTexto
    devolver {(Pila#desdeArreglo: ~t)}#formatear: yo#_elementos
finmetodo

metodo Pila#longitud
    devolver yo#_elementos#longitud
finmetodo

metodo Pila#estáVacía
    devolver yo#longitud = 0
finmetodo

metodo Pila#apilar: valor
    yo#_elementos#agregarAlFinal: valor
finmetodo

metodo Pila#desapilar
    necesitas no yo#estáVacía
    variable res
    fijar res a yo#últimoValorApilado
    yo#_elementos#redimensionar: (yo#_elementos#longitud - 1)
    devolver res
finmetodo

metodo Pila#últimoValorApilado
    necesitas no yo#estáVacía
    devolver yo#_elementos#en: (yo#_elementos#longitud - 1)
finmetodo

metodo Pila#vaciar
    yo#_elementos#redimensionar: 0
finmetodo
